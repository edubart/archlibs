pkgname=openal
pkgver=1.15.1
pkgrel=1
pkgdesc="A cross-platform 3D audio library"
arch=(i686 x86_64)
url="http://www.openal.org/"
license=(LGPL)
depends=(glibc)
makedepends=(alsa-lib sdl pkgconfig cmake libpulse)
options=('!libtool')
source=(http://kcat.strangesoft.net/openal-releases/openal-soft-$pkgver.tar.bz2)
md5sums=('ea83dec3b9655a27d28e7bc7cae9cd71')

build() {
  cd $pkgname-soft-$pkgver

  rm -rf build && mkdir build && cd build
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release -DEXAMPLES=OFF ..
  make
  cd ..

  rm -rf build2 && mkdir build2 && cd build2
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release -DEXAMPLES=OFF -DLIBTYPE=STATIC ..
  make
}

package() {
  cd $pkgname-soft-$pkgver
  make -C build DESTDIR="$pkgdir/" install
  make -C build2 DESTDIR="$pkgdir/" install
}
