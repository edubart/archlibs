_pkgbasename=libogg
pkgname=lib32-$_pkgbasename
pkgver=1.3.0
pkgrel=1
pkgdesc="Ogg bitstream and framing library (32-bit)"
arch=('x86_64')
url="http://www.xiph.org/ogg/"
license=('BSD')
depends=('lib32-glibc' $_pkgbasename)
makedepends=(gcc-multilib)
options=('!libtool')
source=(http://downloads.xiph.org/releases/ogg/$_pkgbasename-$pkgver.tar.gz)
md5sums=('0a7eb40b86ac050db3a789ab65fe21c2')

build() {
    export CFLAGS="${CFLAGS} -march=i686 -m32"
    export CXXFLAGS="${CXXFLAGS} -march=i686 -m32"
    export LDFLAGS="${LDFLAGS} -march=i686 -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd $srcdir/${_pkgbasename}-${pkgver}
    ./configure --enable-shared --enable-static --prefix=/usr --libdir=/usr/lib32
    make
}

package() {
    cd $srcdir/${_pkgbasename}-${pkgver}

    make DESTDIR=$pkgdir install

    rm -rf "$pkgdir"/usr/{share,bin}
    find "$pkgdir"/usr/include/ogg -type f -not -name config_types.h -delete
    mv "$pkgdir"/usr/include/ogg/config_types{,-32}.h
}
