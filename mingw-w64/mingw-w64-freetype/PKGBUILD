# Maintainer: Daniel Kirchner <daniel at ekpyron dot org>

pkgname=mingw-w64-freetype
pkgver=2.5.2
pkgrel=1
pkgdesc="TrueType font rendering library (mingw-w64)"
arch=('any')
url="http://www.freetype.org/"
license=('GPL')
depends=(mingw-w64-zlib mingw-w64-crt)
makedepends=(mingw-w64-gcc)
source=(http://download.savannah.gnu.org/releases/freetype/freetype-$pkgver.tar.gz)
options=(!strip !buildflags !libtool staticlibs)
md5sums=('004320381043d275c4e28bbacf05a1b7')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/build-${_arch}"
    cd "${srcdir}/build-${_arch}"
    CC=${_arch}-gcc ../freetype-${pkgver}/configure --host=${_arch} \
    	--prefix=/usr/${_arch} --enable-static --enable-shared \
    	--with-zlib=/usr/${_arch} --without-png --without-bzip2
    make
  done
}

package () {
  for _arch in ${_architectures}; do
  	cd "${srcdir}/build-${_arch}"
  	make DESTDIR="${pkgdir}" install
  	${_arch}-strip -g "${pkgdir}/usr/${_arch}/lib/"*.a
  	${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
  done
}
