pkgname=mingw-w64-bzip2
pkgver=1.0.6
pkgrel=5
pkgdesc="A high-quality data compression program (mingw-w64)"
arch=(any)
url="http://sources.redhat.com/bzip2"
license=("custom")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt)
options=(!strip !buildflags staticlibs libtool)
source=("http://www.bzip.org/${pkgver}/bzip2-${pkgver}.tar.gz"
"libbz2.la.i686"
"libbz2.la.x86_64")
md5sums=('00b516f4704d4a7cb50a1d97e6e8e15b'
         'af464a968b56ebc00a35c6aa2d8dcfbb'
         '20a73889e7322f6a6afbdf931b9cc929')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cp -r "${srcdir}/bzip2-${pkgver}/"* "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make CC=${_arch}-gcc AR=${_arch}-ar RANLIB=${_arch}-ranlib \
      "CFLAGS=-Wall -Winline -O2 -D_FILE_OFFSET_BITS=64" \
      libbz2.a
    ${_arch}-gcc -shared *.o -o libbz2.dll -Xlinker --out-implib -Xlinker libbz2.dll.a
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    install -Dm644 "libbz2.dll" "${pkgdir}/usr/${_arch}/bin/libbz2.dll"
    install -Dm644 "bzlib.h" "${pkgdir}/usr/${_arch}/include/bzlib.h"
    install -Dm644 "libbz2.a" "${pkgdir}/usr/${_arch}/lib/libbz2.a"
    install -m644 "libbz2.dll.a" "${pkgdir}/usr/${_arch}/lib/libbz2.dll.a"
    install -m644 "$srcdir/libbz2.la.${_arch%-w64-mingw32}" "${pkgdir}/usr/${_arch}/lib/libbz2.la"
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}