_pkgbasename=libvorbis
pkgname=lib32-$_pkgbasename
pkgver=1.3.3
pkgrel=1
pkgdesc="Vorbis codec library (32-bit)"
arch=('x86_64')
license=('custom')
url="http://www.xiph.org/ogg/vorbis/"
depends=('lib32-libogg' $_pkgbasename)
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://downloads.xiph.org/releases/vorbis/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('6b1a36f0d72332fae5130688e65efe1f')

build() {
    export CFLAGS="$CFLAGS -march=i686 -m32"
    export CXXFLAGS="$CXXFLAGS -march=i686 -m32"
    export LDFLAGS="$LDFLAGS -march=i686 -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd ${srcdir}/${_pkgbasename}-${pkgver}
    ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
    make
}

package() {
    cd ${srcdir}/${_pkgbasename}-${pkgver}
    make DESTDIR="${pkgdir}" install
    rm -rf "${pkgdir}"/usr/{include,share,bin}
}
