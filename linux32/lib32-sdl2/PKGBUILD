# Maintainer: Jameson Pugh <imntreal@gmail.com>
# Contributor: J0k3r <moebius282 e4at gmail D0_T com>

pkgname=lib32-sdl2
pkgver=2.0.1
pkgrel=8
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2.0). (32 bit)"
arch=('x86_64')
url="http://www.libsdl.org"
license=('MIT')
depends=('lib32-glibc' 'bash')
options=('!libtool')
conflicts=('lib32-sdl2-hg')
source=("http://www.libsdl.org/release/SDL2-${pkgver}.tar.gz")
sha256sums=('0ae7e902a26777614a011fe7053ca7e8b14843db3c42ca117564d208cf6732f0')

prepare()
{
  mkdir -p "$srcdir/SDL2-${pkgver}/build/"
}

build()
{
  export CFLAGS="${CFLAGS} -march=i686 -m32"
  export CXXFLAGS="${CXXFLAGS} -march=i686 -m32"
  export LDFLAGS="${LDFLAGS} -march=i686 -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "$srcdir/SDL2-${pkgver}/build/"
  ../configure --prefix=/usr --libdir=/usr/lib32
  make
}

package()
{
  cd "$srcdir/SDL2-${pkgver}/build/"
  make DESTDIR="$pkgdir" install

  #already included in sdl2-hg
  rm -rf "$pkgdir/usr/share/aclocal/" "$pkgdir/usr/include/" "${pkgdir}/usr/bin/"
}

# vim:set ts=2 sw=2 et:
