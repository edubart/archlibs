# $Id: PKGBUILD 91894 2013-05-28 20:22:04Z bluewind $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

_pkgbasename=flac
pkgname=lib32-$_pkgbasename
pkgver=1.3.0
pkgrel=1
pkgdesc="Free Lossless Audio Codec (32-bit)"
arch=('x86_64')
url="http://flac.sourceforge.net/"
license=('custom:Xiph' 'LGPL' 'GPL' 'FDL')
depends=('lib32-libogg' 'lib32-gcc-libs' $_pkgbasename)
makedepends=('nasm' gcc-multilib)
options=('!libtool' '!makeflags')
source=(http://downloads.xiph.org/releases/flac/${_pkgbasename}-${pkgver}.tar.xz)
sha1sums=('a136e5748f8fb1e6c524c75000a765fc63bb7b1b')

build() {
  export CFLAGS="${CFLAGS} -march=i686 -m32"
  export CXXFLAGS="${CXXFLAGS} -march=i686 -m32"
  export LDFLAGS="${LDFLAGS} -march=i686 -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man --enable-shared --enable-static \
              --disable-rpath --libdir=/usr/lib32
  make
}

package() {
  cd "${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
