_pkgbasename=openssl
pkgname=lib32-$_pkgbasename
_ver=1.0.1e
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
#pkgver=$_ver
pkgrel=2
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security (32-bit)'
arch=('x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
depends=('lib32-zlib' "${_pkgbasename}")
optdepends=('ca-certificates')
makedepends=('gcc-multilib')
options=('!makeflags')
source=("https://www.openssl.org/source/${_pkgbasename}-${_ver}.tar.gz"
        'no-rpath.patch'
        'ca-dir.patch'
        'openssl-1.0.1e-fix_pod_syntax-1.patch')
md5sums=('66bf6f10f060d561929de96f9dfe5b8c'
         'dc78d3d06baffc16217519242ce92478'
         '3bf51be3a1bbd262be46dc619f92aa90'
         '88d3bef4bbdc640b0412315d8d347bdf')

build() {
    export CFLAGS="${CFLAGS} -march=i686 -m32"
    export CXXFLAGS="${CXXFLAGS} -march=i686 -m32"
    export LDFLAGS="${LDFLAGS} -march=i686 -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cd $srcdir/$_pkgbasename-$_ver

	# remove rpath: http://bugs.archlinux.org/task/14367
	patch -p0 -i $srcdir/no-rpath.patch
	# set ca dir to /etc/ssl by default
	patch -p0 -i $srcdir/ca-dir.patch
  patch -p1 -i $srcdir/openssl-1.0.1e-fix_pod_syntax-1.patch
	# mark stack as non-executable: http://bugs.archlinux.org/task/12434
	# workaround for https://rt.openssl.org/Ticket/Display.html?id=2771
	# See Changes file "Changes between 1.0.1 and 1.0.1a [19 Apr 2012]"
	./Configure --prefix=/usr --openssldir=/etc/ssl --libdir=lib32 \
		shared zlib enable-md2 \
		linux-elf \
		"-Wa,--noexecstack ${CFLAGS} ${LDFLAGS} -DPURIFY" \
		-DOPENSSL_NO_TLS1_2_CLIENT \
		-DOPENSSL_MAX_TLS1_2_CIPHER_LENGTH=50

	make MAKEDEPPROG="gcc -m32" depend
	make
}

check() {
	cd $srcdir/$_pkgbasename-$_ver
	# the test fails due to missing write permissions in /etc/ssl
	# revert this patch for make test
	patch -p0 -R -i $srcdir/ca-dir.patch
	# Do not test TLS1.2 as we disabled it above using OPENSSL_NO_TLS1_2_CLIENT
	sed 's/TLSv1.2 //g' -i test/testssl
	make test
	patch -p0 -i $srcdir/ca-dir.patch
}

package() {
	cd $srcdir/$_pkgbasename-$_ver
	make INSTALL_PREFIX=$pkgdir install

	rm -rf ${pkgdir}/{usr/{include,share,bin},etc}
	mkdir -p $pkgdir/usr/share/licenses
	ln -s $_pkgbasename $pkgdir/usr/share/licenses/$pkgname
}
