_pkgbasename=physfs
pkgname=lib32-physfs
pkgver=2.0.3
pkgrel=2
pkgdesc="A library to provide abstract access to various archives"
arch=('x86_64')
url="http://icculus.org/physfs/"
license=('ZLIB')
depends=('lib32-zlib')
makedepends=('cmake')
source=(http://icculus.org/physfs/downloads/$_pkgbasename-$pkgver.tar.bz2)
sha1sums=('327308c777009a41bbabb9159b18c4c0ac069537')

build() {
    export CFLAGS="-march=i686 -m32 -fno-stack-protector"
    export CXXFLAGS="-march=i686 -m32 -fno-stack-protector"
    export LDFLAGS="-march=i686 -m32 -Wl,--as-needed"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd $srcdir/$_pkgbasename-$pkgver
    cmake . -DCMAKE_BUILD_TYPE=Release \
            -DCMAKE_INSTALL_PREFIX=/usr \
            -DPHYSFS_BUILD_TEST=OFF -DPHYSFS_BUILD_WX_TEST=OFF
    make all
}

package() {
    cd $srcdir/$_pkgbasename-$pkgver
    make DESTDIR="${pkgdir}" install
    mv ${pkgdir}/usr/lib ${pkgdir}/usr/lib32
    rm -rf ${pkgdir}/usr/include
}
