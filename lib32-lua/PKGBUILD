_pkgbasename=lua
pkgname=lib32-${_pkgbasename}
pkgver=5.1.5
pkgrel=1
pkgdesc="A powerful light-weight programming language designed for extending applications" 
arch=('x86_64')
url="http://www.lua.org/"
depends=('lua' 'lib32-glibc' 'lib32-readline' 'lib32-ncurses')
license=('MIT')
options=('!makeflags')
source=(http://www.lua.org/ftp/lua-${pkgver}.tar.gz
        lua-arch.patch lua-5.1-cflags.diff)
md5sums=('2e115fe26e435e33b0d5c022e4490567'
         'fa25feb70ef9fec975b1c20da5098b3c'
         '249582bf1fd861ccf492d2c35a9fe732')

build() {
    export CFLAGS="${CFLAGS} -march=i686 -m32"
    export CXXFLAGS="${CXXFLAGS} -march=i686 -m32"
    export LDFLAGS="${LDFLAGS} -march=i686 -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd "${srcdir}/${_pkgbasename}-${pkgver}"
    patch -p1 -i "${srcdir}/lua-arch.patch"
    patch -p1 -i "${srcdir}/lua-5.1-cflags.diff"

    make INSTALL_DATA="cp -d" TO_LIB="liblua5.1.a liblua5.1.so liblua5.1.so.5.1" \
         INSTALL_TOP="${pkgdir}/usr" MYLDFLAGS="$LDFLAGS" \
         linux
}

package() {
    cd "${srcdir}/lua-${pkgver}"
    make INSTALL_DATA="cp -d" TO_LIB="liblua5.1.a liblua5.1.so liblua5.1.so.5.1 liblua5.1.so.$pkgver" \
         INSTALL_TOP="${pkgdir}/usr" \
         install
    rm -rf ${pkgdir}/usr/{bin,include,share,man}
    mv ${pkgdir}/usr/lib ${pkgdir}/usr/lib32
    install -D -m644 etc/lua.pc "${pkgdir}/usr/lib32/pkgconfig/lua.pc"
}
