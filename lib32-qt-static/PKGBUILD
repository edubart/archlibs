
_pkgbasename=qt
pkgname=lib32-$_pkgbasename-static
pkgver=4.8.4
pkgrel=3
pkgdesc='A cross-platform application and UI framework (32-bit)'
arch=('x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL')
makedepends=(gcc-multilib)
options=('!libtool')
_pkgfqn="${_pkgbasename}-everywhere-opensource-src-${pkgver}"
source=("http://releases.qt-project.org/qt4/source/${_pkgfqn}.tar.gz"
        mingw32-qt-4.8.0-no-webkit-tests.patch)
md5sums=('89c5ecba180cae74c66260ac732dc5cb'
         '91d01b6d31887f78c7933c04544c5758')

build() {
  cd $srcdir/$_pkgfqn

  export QT4DIR=$srcdir/$_pkgfqn
  export LD_LIBRARY_PATH=${QT4DIR}/lib:${LD_LIBRARY_PATH}
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  patch -Np1 < ${srcdir}/mingw32-qt-4.8.0-no-webkit-tests.patch

  # some of those are likely unnecessary, but I'm too lazy to find and remove them
  sed -i "/^QMAKE_LINK\s/s|g++|g++ -m32|g" mkspecs/common/g++-base.conf
  sed -i "s|-O2|${CXXFLAGS} -m32|" mkspecs/common/g++-base.conf
  sed -i "s|-O2|${CXXFLAGS} -m32|" mkspecs/common/gcc-base.conf
  sed -i "/^QMAKE_LFLAGS_RPATH/s| -Wl,-rpath,||g" mkspecs/common/gcc-base-unix.conf
  sed -i "/^QMAKE_LFLAGS\s/s|+=|+= ${LDFLAGS} -m32|g" mkspecs/common/gcc-base.conf
  sed -i "s|-Wl,-O1|-m32 -Wl,-O1|" mkspecs/common/g++-unix.conf
  sed -e "s|-O2|$CXXFLAGS -m32|" \
      -e "/^QMAKE_RPATH/s| -Wl,-rpath,||g" \
      -e "/^QMAKE_LINK\s/s|g++|g++ -m32|g" \
      -e "/^QMAKE_LFLAGS\s/s|+=|+= $LDFLAGS|g" \
      -i mkspecs/common/g++.conf

  ./configure -confirm-license -opensource -v -platform linux-g++-32 \
    -prefix /usr \
    -libdir /usr/lib32 \
    -datadir /usr/share/qt \
    -static \
    -no-phonon \
    -no-phonon-backend \
    -no-webkit \
    -no-qt3support \
    -no-gtkstyle \
    -no-opengl \
    -no-openvg \
    -no-glib \
    -qt-zlib \
    -qt-libtiff \
    -qt-libpng \
    -qt-libmng \
    -qt-libjpeg \
    -qt-sql-sqlite \
    -graphicssystem raster \
    -nomake demos \
    -nomake examples \
    -nomake docs \
    -nomake tools \
    -nomake tests \
    -nomake translations \
    -openssl \
    -silent \
    -no-rpath \
    -dbus \
    -fast

  make
}

package() {
  cd $srcdir/$_pkgfqn
  make INSTALL_ROOT=$pkgdir install

  # Fix wrong path in pkgconfig files
  find ${pkgdir}/usr/lib32/pkgconfig -type f -name '*.pc' \
    -exec perl -pi -e "s, -L${srcdir}/?\S+,,g" {} \;
  # Fix wrong path in prl files
  find ${pkgdir}/usr/lib32 -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;

  rm -rf "${pkgdir}"/usr/{include,share,bin,tests,plugins,imports}
  rm -rf "${pkgdir}"/usr/lib32/pkgconfig
  rm -rf "${pkgdir}"/usr/lib32/*.prl
}
